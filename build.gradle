// Top-level build file where you can add configuration options common to all sub-projects/modules
apply plugin: 'com.android.library'

android {
    compileSdkVersion 33
    testOptions.unitTests.includeAndroidResources = true
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 4
        versionName "1.0.0"
        buildConfigField 'String', 'VERSION_NAME', "\"${version}\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        abortOnError false
        warningsAsErrors false
        lintConfig file("config/lint.xml")
    }
}


buildscript {
    repositories {
        google()
        mavenCentral()
        mavenLocal()

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.4.2'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7"
        classpath "org.jacoco:org.jacoco.core:0.8.8"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        mavenLocal()

    }

    project.version = "1.0.0-beta14"

}

subprojects {

    ext {
        hyperwalletGroupId = 'com.hyperwallet.android.ui'

        compileVersion = 33
        minVersion = 21
        targetVersion = 33
        codeVersion = 1

        hyperwalletCoreVersion = '1.0.0-beta12'
        hyperwalletInsightVersion = '1.0.0-beta02'
        //
        androidMaterialVersion = '1.4.0'
        appcompatVersion = '1.3.1'
        constraintlayoutVersion = '1.1.3'
        legacySupportV4Version = '1.0.0'
        recycleViewVersion = '1.2.1'
        lifecycleExtensionsVersion = '2.0.0'
        pagingRuntimeVersion = '2.1.0'
        //Testing
        extJunitVerson = '1.1.3'
        testRunnerVersion = '1.2.0'
        testRulesVersion = '1.2.0'
        espressoVersion = '3.4.0'
        mockServerVersion = '3.11.0'
        leakcanaryVersion = '1.6.3'
        mockitoVersion = '5.15.2'
        junitParamsVersion = '1.1.1'
        robolectricVersion = '4.14.1'
        coreTest = '1.4.0'
        mockWebServerVersion = '4.12.0'
        //
        jacocoVersion = "0.8.8"
        fileFilter = ['**/BuildConfig.*']
    }

}

task customClean(type: Delete) {
    delete rootProject.buildDir
}

task generateAggregatedJavadocs(type: Javadoc) {

    options.links 'https://hyperwallet.github.io/hyperwallet-android-sdk/',
            'http://docs.oracle.com/javase/7/docs/api/'
    options.addStringOption 'Xdoclint:none', '-quiet'

    def subs = rootProject.subprojects.findAll {sub -> sub.name != 'testutils'}
    rootProject.gradle.projectsEvaluated {
        rootProject.task("allJavadocs") {
            description = 'Generate an aggregated javadoc for all projects'
            group = "documentation"
            dependsOn subs.javadocs
            source subs.javadocs.source
            destinationDir rootProject.file("$rootProject.buildDir/docs/javadoc")
            classpath = rootProject.files(subs.javadocs.classpath)
        }
    }
}
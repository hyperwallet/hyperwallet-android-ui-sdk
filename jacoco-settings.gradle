apply plugin: 'jacoco'

jacoco {
    toolVersion = project.ext.jacocoVersion
}

android {
    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

def debugClassPaths = [
        '**/intermediates/javac/debug/*/classes/**'
]

final def coverageSourceDirs = ["$project.projectDir/src/main/java/*"]

task jacocoTestReport(type: JacocoReport, dependsOn: 'testDebugUnitTest') {

    group = 'Reporting'
    description = 'Generate Jacoco coverage reports.'

    reports {
        html {
            enabled = true
            destination file("$buildDir/reports/jacoco")
        }
    }

    classDirectories.setFrom(fileTree(
            dir: "${buildDir}",
            includes: debugClassPaths,
            excludes: project.ext.fileFilter
    )
    )

    additionalSourceDirs.setFrom(files(coverageSourceDirs))
    sourceDirectories.setFrom(files(coverageSourceDirs))
    executionData.setFrom(files("${buildDir}/jacoco/testDebugUnitTest.exec"))
}

task jacocoTestCoverageVerification(type: JacocoCoverageVerification, dependsOn: 'jacocoTestReport') {

    group = 'Verification'
    classDirectories.setFrom(fileTree(
            dir: "${buildDir}",
            includes: debugClassPaths,
            excludes: fileFilter
    ))
    additionalSourceDirs.setFrom(files(coverageSourceDirs))
    sourceDirectories.setFrom(files(coverageSourceDirs))
    executionData.setFrom(files("${buildDir}/jacoco/testDebugUnitTest.exec"))

    violationRules {
        setFailOnViolation(true)

        rule {
            element = 'CLASS'
            limit {
                value = 'COVEREDRATIO'
                counter = 'BRANCH'
                minimum = 0.65
            }
        }
    }
}

